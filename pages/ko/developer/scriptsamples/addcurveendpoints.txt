====== 커브의 끝점에 점을 추가하는 방법 ======
====== RhinoScript ======
> **Version:** //Rhino 4.0//
> **Summary:** //커브의 시작점과 끝점에 점을 추가하는 방법을 소개합니다.//

=====질문=====
모든 커브의 끝점에 점을 배치하는 스크립트를 찾고 있습니다 (예: 5000 개의 커브, 10,000 개의 끝점). 어떻게 하면 되나요?

=====답변=====
다음 스크립트를 사용하여 이 작업을 실행할 수 있습니다:


<code vb>
 Option Explicit

 Sub AddCurveEndPoints()

   Const rhCurve = 4

   ' Get all the curve objects in the document
   Dim arrCurves
   arrCurves = Rhino.ObjectsByType(rhCurve)
   If IsNull(arrCurves) Then Exit Sub

   ' For better performance, turn off screen redrawing  
   Call Rhino.EnableRedraw(False)

   ' Process each curve       
   Dim strCurve
   For Each strCurve In arrCurves
     ' Add a point at the start of the curve
     Call Rhino.AddPoint(Rhino.CurveStartPoint(strCurve))
     ' If not closed, add a point at the end of the curve
     If Not Rhino.IsCurveClosed(strCurve) Then
       Call Rhino.AddPoint(Rhino.CurveEndPoint(strCurve))
     End If
   Next

   ' Turn screen redrawing back on  
   Call Rhino.EnableRedraw(True)

 End Sub


</code>


{{tag>ko:Developer ko:RhinoScript}}
